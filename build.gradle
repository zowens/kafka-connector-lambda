buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

repositories {
    jcenter()
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'

group = "io.github.zowens.${rootProject.name}"
version = '0.1.0'


sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    kafkaConnectVersion = '1.0.0'
    awsSdkVersion = '1.11.271'
    guavaVersion = '24.0-jre'
    jacksonVersion = '2.9.4'
    slf4jVersion = '1.7.7'
    failsafeVersion = '1.0.5'
}

jar {
    manifest {
        attributes 'Implementation-Title': group, 'Implementation-Version': version
    }
}

dependencies {
    compile "org.apache.kafka:connect-api:${kafkaConnectVersion}"
    compile "com.amazonaws:aws-java-sdk-lambda:${awsSdkVersion}"
    compile "com.google.guava:guava:${guavaVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-guava:${jacksonVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "net.jodah:failsafe:${failsafeVersion}"

    testCompile 'junit:junit-dep:4.10'
    testCompile 'org.mockito:mockito-all:1.10.0'
    testRuntime "org.slf4j:slf4j-simple:${slf4jVersion}"
}

test {
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "TRACE"
    systemProperty "org.slf4j.simpleLogger.showDateTime", "false"
}

jar {
    manifest {
        attributes(
            "Version": project.version,
            "Connect-Version" : "${kafkaConnectVersion}",
        )
    }
}

shadowJar {
    configurations = [project.configurations.runtime]
    baseName = "${project.name}-${project.version}-all"
    classifier = null
    version = null

    zip64 true

    manifest {
        attributes(
            "Version": project.version,
            "Connect-Version": "${kafkaConnectVersion}",
        )
    }

    mergeServiceFiles {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    dependencies {
        exclude(dependency("org.apache.avro:.*"))
        exclude(dependency("org.apache.kafka:.*"))
        exclude(dependency("io.confluent:.*"))
        exclude(dependency("org.apache.kafka:.*"))
        exclude(dependency("org.apache.zookeeper:.*"))
    }
}

// run ShadowJ Jar after Build
build.finalizedBy(shadowJar)
